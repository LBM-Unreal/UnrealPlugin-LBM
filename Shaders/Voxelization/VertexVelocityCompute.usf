// VertexVelocityCompute.usf
#include "/Engine/Public/Platform.ush"

// Input buffers
Buffer<float> TriangleVerts;	// Mesh vertex buffer SRV

RWStructuredBuffer<float3> VertexPositionsWorldSpace;
RWStructuredBuffer<float3> VertexVelocitiesWorldSpace;

// Parameters
float4x4 LocalToWorld;
uint VertexCount;
float DeltaTime;

void LoadVertexPosition(in uint vertex, out float3 pos)
{
	vertex = clamp(vertex, 0, VertexCount - 1);
	pos.x = TriangleVerts[vertex * 3 + 0];
	pos.y = TriangleVerts[vertex * 3 + 1];
	pos.z = TriangleVerts[vertex * 3 + 2];
}

[numthreads(64, 1, 1)]
void CalculateVertexVelocity(uint3 DTid : SV_DispatchThreadID)
{
	if (DTid.x >= VertexCount) { return; }

    float deltaTime = max(0.01f, DeltaTime);

	float3 localPos;
	LoadVertexPosition(DTid.x, localPos);

	float4 worldPos = mul(LocalToWorld, float4(localPos, 1.0));
    float3 worldPosPrevFrame = VertexPositionsWorldSpace[DTid.x];
    float3 velocity = (worldPos.xyz - worldPosPrevFrame) / deltaTime;
	
	// Write outputs
	VertexVelocitiesWorldSpace[DTid.x] = velocity;
    VertexPositionsWorldSpace[DTid.x] = worldPos.xyz;
}
